plugins {
    id 'groovy'
    id 'java-library'
    id "nebula.resolution-rules" version "7.8.0"
}

repositories {
    mavenCentral()
    /**
     * Sample repo that contains `netflix:my-library:1.0.0`
     */
    maven {
        url = 'repo'
    }
}

dependencies {
    /**
     * This loads resolution rules from a file within the repo
     * We have OSS rules in https://github.com/nebula-plugins/gradle-resolution-rules/tree/master/src/main/resources. You can apply them via:
     *     resolutionRules 'com.netflix.nebula:gradle-resolution-rules:latest.release'
     * You can also upload a JAR or ZIP files with json files and consume them as a dependency also
     *     resolutionRules 'com.myorg:resolution-rules:latest.release'
     */
    resolutionRules files('my-resolution-rules.json')

    implementation 'org.codehaus.groovy:groovy-all:2.5.13'

    /**
     * This should be substituted to guava 30.1-jre
     */
    implementation 'com.google.guava:guava:29.0-jre'

    /**
     * Libraries from this group (family) should be aligned to avoid conflict
     */
    implementation 'com.amazonaws:aws-java-sdk-s3:1.11.931'
    implementation 'com.amazonaws:aws-java-sdk-sqs:1.11.946'

    /**
     * Replacement rules act only when both modules are in the graph
     * netflix:my-library:1.0.0 brings javax.persistence:javax.persistence-api:2.0 which should be replaced by jakarta.persistence:jakarta.persistence-api:3.0.0
     */
    implementation 'netflix:my-library:1.0.0'
    implementation 'jakarta.persistence:jakarta.persistence-api:3.0.0'

    /**
     * When this is enabled, the build should fail because we are denying bcmail-jdk15 candidates
     */
    // implementation 'bouncycastle:bcmail-jdk15:140'

    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'junit:junit:4.13'

    api 'org.apache.commons:commons-math3:3.6.1'
}
