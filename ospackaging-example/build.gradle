plugins {
    id 'groovy'
    id 'application'
    id "nebula.ospackage" version "8.5.0"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:2.5.13'
    implementation 'com.google.guava:guava:29.0-jre'
}

application {
    mainClass = 'ospackaging.example.App'
}

ospackage {
    packageName = 'mysuperapp'
    version = '1.0.0'
    release = '1'

    /**
     * Required packages
     */
    requires('someInternalPackage')
    requires('ffmpeg')

    /**
     * Adding pre and post install scripts
     */
    preInstall file('scripts/preInstall.sh')
    postInstall file('scripts/postInstall.sh')

    /**
     * We will copy into this folder
     */
    into '/etc/mysuperapp'

    from(jar.outputs.files)

    /**
     * Copy runtime classpath libraries into lib folder
     */
    from(configurations.runtimeClasspath) {
        into 'lib'
    }

    /**
     * Copy resources under conf folder
     */
    from('src/main/resources') {
        fileType CONFIG | NOREPLACE
        into 'conf'
    }
}
